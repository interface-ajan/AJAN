' ================================================================================
' C言語連携＞UART通信テスト＞hd_send サンプルプログラム 
'  C言語連携機能を使って、GPG-4141のシリアル通信ライブラリを呼び出すサンプルプログラムです。
'  GPG-4141付属の fd_send.c サンプルの移植例です。
'  全二重方式でメッセージを送信します。入力した文字列を都度送信します。
'				Copyright 2022 Interface Corporation
' ================================================================================

include "GPG4141_EX.AJN"

' GPG-4141の呼び出しで必要なプロトタイプ宣言
CDECLARE "libc.so.6", GPG4141_PROTOTYPE$

' C言語で書いたサブルーチンを組み込みます
CIMPORT "CFUNC_GPG4141_sub.c"
' C言語で書いたサブルーチンを、AJANから呼び出せるようにプロトタイプ宣言を読み取ります
s$ = '''
int c_get_exitflag();
void c_sigaction_SIGQUIT();
int c_open_cp4141port(int DeviceID, int SubsystemID, int BoardID, int Channel);
'''
CDECLARE "", s$

CLS
' c_open_cp4141port は、PCI-4142系を探索して見つかったらオープンする
errno = CFUNCALL(hPort%, "c_open_cp4141port", 4142, 1, 0, 1)
if hPort% < 0 then
	' デバイスが見つからない場合、手動でデバイス名を入れてもらう
	INPUT "Input the  One  Port Device Name : "; DeviceName$
	errno = CFUNCALL(hPort%, "open", DeviceName$, O_RDWR or O_NDELAY )				' Open device 
	? "Open="; errno, Handle%
end if
if hPort% < 0 then
	PRINT "The port is not found ", errno, hPort%
	end
end if

errno = CSUBCALL("c_sigaction_SIGQUIT")

errno = CFUNCALL(Ret%, "tcflush", hPort%, TCOFLUSH)
OBJECT tty_config@ as MEMORY
tty_config@ = CSTRUCT@("termios")
errno = CFUNCALL(Ret%, "tcgetattr", hPort%, tty_config@)

v% = tty_config@.GETMEMVAL("c_cflag")
tty_config@.SETMEMVAL("c_cflag", v% and not (CRTSCTS& or CSIZE))
v% = tty_config@.GETMEMVAL("c_cflag")
tty_config@.SETMEMVAL("c_cflag", v% or      (CS8 or CLOCAL or CREAD))
v% = tty_config@.GETMEMVAL("c_iflag")
tty_config@.SETMEMVAL("c_iflag", v% and not (IXON or IXOFF or ICRNL or ISTRIP))
tty_config@.SETMEMVAL("c_oflag", 0)
v% = tty_config@.GETMEMVAL("c_lflag")
tty_config@.SETMEMVAL("c_lflag", v% and not (ICANON or ECHO))

errno = CFUNCALL(Ret%, "tcsetattr", hPort%, TCSANOW, tty_config@)
if Ret% <> 0 then
	PRINT "tcsetattr error"
	end
end if

BaudRate% = 10000		' BAUDRATE;
errno = CFUNCALL(Ret%, "ioctl", hPort%, CP4141_SET_BAUDRATE, BaudRate%)
if Ret% <> 0 then
	PRINT "ioctl(CP4141_SET_BAUDRATE) error"
	errno = CFUNCALL(Ret%, "close", hPort%)
	end
end if

DuplexMode% = CP4141_FULL_DUPLEX
errno = CFUNCALL(Ret%, "ioctl", hPort%, CP4141_SET_DUPLEX_MODE, DuplexMode%)
if Ret% <> 0 then
	PRINT "ioctl(CP4141_SET_DUPLEX_MODE) error"
	errno = CFUNCALL(Ret%, "close", hPort%)
	end
end if

DO WHILE TRUE
	errno = CFUNCALL(Ret%, "c_get_exitflag")
	if Ret% = 0 then
		EXIT DO
	end if
	
	' getcharの代わりにINPUT。1文字ずつ入力できないが同じように改行ごとに反応する
	INPUT "send message:"; buffer$
	if LENB(buffer$) > 0 then
		errno = CFUNCALL(Ret%, "write", hPort%, buffer$, LENB(buffer$))
	end if
LOOP

PRINT "terminate program."

errno = CFUNCALL(Ret%, "close", hPort%)

? "bye.bye"
end
