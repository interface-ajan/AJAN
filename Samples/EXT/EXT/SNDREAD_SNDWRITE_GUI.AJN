' ================================================================================
' SNDOPEN / SNDREAD / SNDWRITE / SNDCLOSE サンプルプログラム(GUI版)
'  SNDREAD_SNDWRITE.AJN サンプルプログラムのGUI版です。
'  オーディオ入力からサンプリング入力(SNDREAD関数)しつつ、
'  データを波形グラフ表示し、同じデータをサンプリング出力(SNDWRITE命令)します。
'				Copyright 2019 Interface Corporation
' ================================================================================
OPTION GUI

' オーディオ入出力のバッファ
LIST BUF

' オーディオ入力→グラフ描画→オーディオ出力するスレッド処理
DEFINE THREAD SND_TH(NUM%)

	' オーディオ入力をオープン
	SNDOPEN "?RATE=8000&FORMAT=FLOAT32LE" FOR INPUT AS #1
	' オーディオ出力をオープン
	SNDOPEN "?RATE=8000&FORMAT=FLOAT32LE" FOR OUTPUT AS #2
	
	DO WHILE (IS_EXIT = FALSE)
		' オーディオ入力から256件をサンプリング入力
		LOCK
		BUF = SNDREAD(1, 256)
		UNLOCK

		' サンプリング入力した事を通知 -> 描画してもらう
		IS_SAMPLED = TRUE

		PRINT "オーディオ入力件数="; LDIM(BUF)

		' オーディオ出力に対して、入力したデータをサンプリング出力
		SNDWRITE #2, BUF
		PRINT "オーディオ出力:OK"
	LOOP
	
	' オーディオ入力をクローズ
	SNDCLOSE #1
	' オーディオ出力をクローズ
	SNDCLOSE #2

END THREAD

' ****** メイン処理 ******

' GUIウィンドウを作成
GUCREATE WINDOW 1, "サンプル",1,,"400,200",,
GUCREATE LINEGRAPH 1, 10,,"400,200",,
' ウィンドウを表示
GUSHOW 1

' オーディオ処理を行うスレッドを呼び出す
ATTACH THREAD 1, SND_TH

' 3秒間ほど待機
ST = CLOCK
DO WHILE (CLOCK - ST) < 3.0
	IF IS_SAMPLED = TRUE THEN
		' サンプリング入力したデータを、波形グラフ表示
		LOCK
		GUUPD LINEGRAPH VALUE 10, ( BUF ), 2, 2
		UNLOCK
		GUUPD LINEGRAPH DRAW 10
		
		IS_SAMPLED = FALSE
	ELSE
		' GUI描画を
		F = GUMAIN ONELOOP(0)
	END IF
LOOP

' IS_EXIT = TRUE にして、スレッドを終わるよう依頼
IS_EXIT = TRUE
SLEEP 0.5

' GUIウィンドウを閉じる
GUDELETE WINDOW 1

END
