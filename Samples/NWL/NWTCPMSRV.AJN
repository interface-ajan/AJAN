' ================================================================================
' TCPサーバーのサンプルプログラム(複数クライアントから接続できる。行単位に処理する)
'  NWTCPSRV.AJNのサーバー処理の、マルチスレッド対応版です。
'  NWTCPMSRV.AJNを実行してから、別の端末でNWTCPMCLI.AJNを実行します。
'  複数のクライアントからの接続に同時対応できます。
'  NWACCEPT関数で、クライアントから接続されると、
'  スレッドを生成して、クライアントとの通信処理を別スレッドに任せます。
'  スレッド内では、データを受信したら送り返すのを、3回繰り返して終了します。
'  15回、クライアントから接続されると、プログラムを終了します。
'              Copyright 2018 Interface Corporation
' ================================================================================

' 画面クリア
CLS

? "ネットワークをオープン"
NWOPEN "TCP:127.0.0.1:2000" FOR SERVER AS #1

EXEC_THREAD_CNT = 0

' 15回分、クライアントからの接続受付する
' (実用に用いるには、空いているスレッドに割り振りするなどの処理を加えると望ましいです)
FOR TH_CNT%=1 TO 15

	? "クライアントからの接続待ち"
	CNUM% = NWACCEPT(1)
	? "クライアント番号:"; CNUM%
	
	? "スレッド番号:"; TH_CNT%; "で送受信処理を起動します"
	EXEC_THREAD_CNT = EXEC_THREAD_CNT + 1	' スレッド実行のカウントを上げる
	ATTACH THREAD TH_CNT%, SUB_RW( CNUM% )

NEXT TH_CNT%

DO WHILE EXEC_THREAD_CNT > 0
	' スレッド SUB_RWが実行中の間、ループして処理が終わらないようにする
LOOP

? "ネットワークを閉じる"
NWCLOSE #1

? "bye.bye"
END

DEFINE THREAD SUB_RW(NUM%, CNUM%)
	? "スレッドを実行します。スレッド番号:"; NUM%; " クライアント番号:"; CNUM%
	LOCAL CNT, A$

	' 以下の処理を3回繰り返す
	FOR CNT=1 TO 3
	
		' 受信処理 - 改行コード単位に読み取る指定付き(-1)
		A$ = NWRECV$(CNUM%, -1, 1)
		? "受信結果:["; A$; "]"
		
		' 送信処理 - 受信結果を返信して返す。
		NWSEND CNUM%, A$
		? "返信した:["; A$; "]"
	
	NEXT CNT
	
	? "クライアント("; CNUM%; ")との接続を閉じる"
	NWCLOSE CNUM%

	' スレッド実行終了なので、カウントを下げる
	EXEC_THREAD_CNT = EXEC_THREAD_CNT - 1
END THREAD
